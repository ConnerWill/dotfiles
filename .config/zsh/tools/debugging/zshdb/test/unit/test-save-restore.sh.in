#!@SH_PROG@
# -*- shell-script -*-

test_save_restore_is_unsetopt()
{
    _Dbg_is_unsetopt shwordsplit
    assertFalse 'shwordsplit should be set' $?
    unsetopt ksharrays
    _Dbg_is_unsetopt ksharrays
    assertTrue 'ksharrays should be set' $?
    setopt ksharrays
    _Dbg_is_unsetopt ksharrays
    assertFalse 'ksharrays should not be set' $?
}

test_save_restore_create_unsetopt()
{
    _Dbg_restore_unsetopt='foo'
    _Dbg_restore_setopt='bar'
    typeset opts=' cshjunkiehistory cshjunkieloops cshjunkiequotes cshnullcmd'
    unsetopt $opts
    _Dbg_create_unsetopt $opts
    assertEquals "$opts" "$_Dbg_restore_unsetopt"
    assertEquals '' "$_Dbg_restore_setopt"
    setopt localtraps
    _Dbg_create_unsetopt $opts localtraps
    assertEquals "$opts" "$_Dbg_restore_unsetopt"
    assertEquals ' localtraps' "$_Dbg_restore_setopt"
}

# Test that we are saving and restoring POSIX variables IFS and PS4 that
# the debugger changes.
test_save_restore_IFS_PS4()
{
    typeset  _Dbg_space_IFS=' '
    typeset old_IFS="$IFS"
    typeset new_ifs=' 	'
    IFS="$new_ifs"
    PS4='123'
    _Dbg_set_debugger_entry
    if [[ -z "$TRAVIS" ]] ; then
	assertEquals "$_Dbg_space_IFS" "$IFS"
    fi
    assertNotEquals '123' "$PS4"
    _Dbg_set_to_return_from_debugger
    if [[ -z "$TRAVIS" ]] ; then
	assertNotEquals "$_Dbg_space_IFS" "$IFS"
	assertEquals "$new_ifs" "$IFS"
    fi
    IFS="$old_IFS"
}

# Test that we are saving and restoring zsh option ksharrays that
# the debugger changes.
test_save_restore_ksharrays()
{
    # Try without ksharrays set
    unsetopt ksharrays
    _Dbg_old_set_opts=$-
    typeset -a aa
    _Dbg_set_debugger_entry 'save_opts'
    _Dbg_set_to_return_from_debugger
    aa=(one two three)
    typeset test_value=$aa[1]
    assertEquals 'one' "$test_value"

    # Try with ksharrays set
    setopt ksharrays
    _Dbg_set_debugger_entry 'save_opts'
    _Dbg_set_to_return_from_debugger
    aa=(one two three)
    typeset test_value=${aa[1]}
    assertEquals 'two' "$test_value"
}

# Test that we are saving and restoring zsh option shwordsplit that
# the debugger changes.
test_save_restore_shwordsplit()
{
    # Try without shwordsplit set
    unsetopt ksharrays shwordsplit
    _Dbg_old_set_opts=$-
    _Dbg_restore_unsetopt=''
    typeset test_string='one two three'
    typeset -a aa
    _Dbg_set_debugger_entry 'save_opts'
    _Dbg_set_to_return_from_debugger
    aa=($test_string)
    typeset test_value=$aa[1] # $aa[1] won't work if ksharrays is set
    setopt shwordsplit # Needed for assertEquals
    assertEquals "$test_string" "$test_value"

    # Try with shwordsplit set. It was set just before assertEquals
    _Dbg_set_debugger_entry 'save_opts'
    _Dbg_set_to_return_from_debugger
    aa=($test_string)
    typeset test_value=$aa[1] # $aa[1] won't work if ksharrays is set
    assertEquals 'one' "$test_value"
}

# Make sure ../.. has a trailing slash
abs_top_srcdir=@abs_top_srcdir@
abs_top_srcdir=${abs_top_srcdir%%/}/
. ${abs_top_srcdir}test/unit/helper.sh

. $abs_top_srcdir/dbg-pre.sh
. $abs_top_srcdir/lib/journal.sh
. $abs_top_srcdir/lib/save-restore.sh
set -- # reset $# so shunit2 doesn't get confused.

SHUNIT_PARENT=$0

[[ @CMDLINE_INVOKED@ ]] && . ${shunit_file}
